# 1) workAuthority for utgivelser med creator

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX duo: <http://data.deichman.no/duo#>

WITH <http://deichman.no/migration>

INSERT {
  ?publication migration:workAuthority ?sum .
}

WHERE {
 { SELECT SUM(xsd:int(?id)) AS ?sum ?publication WHERE {
    ?publication migration:creator ?agent ;
      a :Publication .
    ?agent duo:bibliofilAuthorityId ?id .
  }}
}
;


# 2) workAuthority for utgivelser med editor, uten creator

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX duo: <http://data.deichman.no/duo#>
PREFIX role: <http://data.deichman.no/role#>

WITH <http://deichman.no/migration>

INSERT {
  ?publication migration:workAuthority ?sum .
}

WHERE {
 { SELECT SUM(xsd:int(?id)) AS ?sum ?publication WHERE {
   ?publication role:editor ?agent ;
      a :Publication .
    ?agent duo:bibliofilAuthorityId ?id .
    MINUS {?publication migration:creator ?creator .}
  }}
}
;


# 3) workAuthority for utgivelser med director, uten creator og editor

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX duo: <http://data.deichman.no/duo#>
PREFIX role: <http://data.deichman.no/role#>

WITH <http://deichman.no/migration>

INSERT {
  ?publication migration:workAuthority ?sum .
}

WHERE {
 { SELECT SUM(xsd:int(?id)) AS ?sum ?publication WHERE {
    ?publication role:director ?agent ;
     a :Publication .
    ?agent duo:bibliofilAuthorityId ?id .
    MINUS {?publication migration:creator ?creator .}
    MINUS {?publication role:editor ?editor .}
  }}
}
;


# 4) workAuthority for utgivelser med composer, uten creator, editor og director

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX duo: <http://data.deichman.no/duo#>
PREFIX role: <http://data.deichman.no/role#>

WITH <http://deichman.no/migration>

INSERT {
  ?publication migration:workAuthority ?sum .
}

WHERE {
 { SELECT SUM(xsd:int(?id)) AS ?sum ?publication WHERE {
    ?publication role:composer ?agent ;
     a :Publication .
   ?agent duo:bibliofilAuthorityId ?id .
   MINUS {?publication migration:creator ?creator .}
   MINUS {?publication role:editor ?editor .}
   MINUS {?publication role:director ?director .}
  }}
}
;


# 5) Verk fra utgivelser uten originaltittel

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX duo: <http://data.deichman.no/duo#>

WITH <http://deichman.no/migration>

INSERT {
  `IRI(CONCAT(STR("http://__HOST__/work/w"), MD5(CONCAT(STR(?workAuthority), ?mainTitle, ?partNumber, ?partTitle))))` a :Work ;
    :mainTitle ?mainTitle ;
    :partNumber ?partNumber ;
    :partTitle ?partTitle .
  ?publication :publicationOf `IRI(CONCAT(STR("http://__HOST__/work/w"), MD5(CONCAT(STR(?workAuthority), ?mainTitle, ?partNumber, ?partTitle))))` .
}

WHERE {
  ?publication migration:workAuthority ?workAuthority ;
    a :Publication ;
    :mainTitle ?mainTitle .
  MINUS { ?publication migration:originalTitle ?originalTitle }
  OPTIONAL { ?publication :partNumber ?partNumber }
  OPTIONAL { ?publication :partTitle ?partTitle }
}
;


# 6) Verk fra utgivelser med originaltittel

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX util: <http://data.deichman.no/utility#>
PREFIX duo: <http://data.deichman.no/duo#>

WITH <http://deichman.no/migration>

INSERT {
  `IRI(CONCAT(STR("http://__HOST__/work/w"), MD5(CONCAT(STR(?workAuthority), ?mainTitle, ?partNumber, ?partTitle))))` a :Work ;
    :mainTitle ?mainTitle ;
    :partNumber ?partNumber ;
    :partTitle ?partTitle .
  ?publication :publicationOf `IRI(CONCAT(STR("http://__HOST__/work/w"), MD5(CONCAT(STR(?workAuthority), ?mainTitle, ?partNumber, ?partTitle))))` .
}

WHERE {
  ?publication migration:workAuthority ?workAuthority ;
    migration:originalTitle ?mainTitle ;
    a :Publication .
  OPTIONAL { ?publication :originalPartNumber ?partNumber }
  OPTIONAL { ?publication :originalPartTitle ?partTitle }
}
;


# X) Create work to any publications not belonging to any work (i.e not matched by above queries)
SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX util: <http://data.deichman.no/utility#>
PREFIX duo: <http://data.deichman.no/duo#>
PREFIX role: <http://data.deichman.no/role#>

WITH <http://deichman.no/migration>

INSERT {
  `IRI(CONCAT(STR("http://__HOST__/work/w"), MD5(CONCAT(STR(?contributors), ?mainTitle, ?partNumber, ?partTitle))))` a :Work ;
    :mainTitle ?mainTitle ;
    :partTitle ?partTitle ;
    :partNumber ?partNumber .
  ?publication :publicationOf `IRI(CONCAT(STR("http://__HOST__/work/w"), MD5(CONCAT(STR(?contributors), ?mainTitle, ?partNumber, ?partTitle))))` .
}

WHERE {
  {
    SELECT SUM(xsd:int(?contributor_id)) as ?contributors ?publication ?mainTitle ?partNumber ?partTitle
    WHERE {
        ?publication a :Publication ;
                     :mainTitle ?mainTitle .
        OPTIONAL { ?publication role:contributor ?contributor .
                   ?contributor duo:bibliofilPersonId ?contributor_id .}
        OPTIONAL { ?publication role:contributor ?contributor .
                   ?contributor duo:bibliofilCorporationId ?contributor_id .}
        OPTIONAL { ?publication :partNumber ?partNumber }
        OPTIONAL { ?publication :partTitle ?partTitle }
        MINUS { ?publication :publicationOf ?work }
    }
  }
}
;