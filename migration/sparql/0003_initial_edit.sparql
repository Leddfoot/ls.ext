# 1) Merge locationCategory code 'a' into locationFormat

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?publication :locationFormat ?format .
}

INSERT {
  ?publication :locationFormat `concat("a",str(?format))` .
}

WHERE {
  ?publication migration:locationCategory "a" ;
    a :Publication .
  OPTIONAL {?publication :locationFormat ?format .}
}
;


# 2) Add parent titles

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>

WITH <http://deichman.no/migration>

INSERT {
	?publication :mainTitle ?title .
}

WHERE {
	?publication migration:parentRecordTitle ?title .
	MINUS {?publication :mainTitle ?o .}
}
;


# 3) Add main titles from parent records

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>

WITH <http://deichman.no/migration>

INSERT {
	?publication :mainTitle ?title .
}

WHERE {
	?publication migration:parentRecordId ?id .
	MINUS {?publication :mainTitle ?o .}
	?parent :recordId ?id ;
		:mainTitle ?title .
}
;


# 4) Construct music cd classification with source

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX raw: <http://data.deichman.no/raw#>

WITH <http://deichman.no/migration>

INSERT {
	?publication migration:classification `IRI(CONCAT("http://data.deichman.no/classification/x", MD5(STR(?notation))))` .
	`IRI(CONCAT("http://data.deichman.no/classification/x", MD5(STR(?notation))))` raw:classificationNotation ?notation ;
		raw:classificationNotation "cdclass" .
}

WHERE {
	?publication migration:musicClass ?notation .
	FILTER (!REGEX(?notation, "^[\\d]{3}"))
}
;


# 5) Construct music dewey classification

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX raw: <http://data.deichman.no/raw#>

WITH <http://deichman.no/migration>

INSERT {
	?publication migration:classification `IRI(CONCAT("http://data.deichman.no/classification/x", STR(?notation)))` .
	`IRI(CONCAT("http://data.deichman.no/classification/x", STR(?notation)))` raw:classificationNotation ?notation .
}

WHERE {
	?publication migration:musicClass ?notation .
	FILTER (REGEX(?notation, "^[\\d]{3}"))
}
;
