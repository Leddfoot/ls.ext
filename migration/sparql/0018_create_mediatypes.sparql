# 1) Construct media type based on format, keep format

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX format: <http://data.deichman.no/format#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>

WITH <http://deichman.no/migration>

INSERT {
  ?pub :mediaType ?mediaType
}

WHERE {
    ?pub a :Publication ;
         :format ?format  .

    VALUES (?format ?mediaType) {
      (format:Book                    mediaType:Book)
      (format:E-Book                  mediaType:Book)
      (format:DAISY                   mediaType:Audiobook)
      (format:Videotape               mediaType:Film)
      (format:DVD                     mediaType:Film)
      (format:Blu-ray_Disk            mediaType:Film)
      (format:Blu-ray_ROM             mediaType:Game)
      (format:Personal_computer_game  mediaType:Game)
      (format:Playstation_2_game      mediaType:Game)
      (format:Playstation_3_game      mediaType:Game)
      (format:Playstation_4_game      mediaType:Game)
      (format:Xbox_360_game           mediaType:Game)
      (format:Xbox_One_game           mediaType:Game)
      (format:Nintendo_DS_game        mediaType:Game)
      (format:Nintendo_Wii_game       mediaType:Game)
    }
}
;

# 2) Replace format with Media Type

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX format: <http://data.deichman.no/format#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>

WITH <http://deichman.no/migration>

MODIFY 

DELETE {
  ?pub :format ?format .
}

INSERT {
  ?pub :mediaType ?mediaType
}

WHERE {
    ?pub a :Publication ;
         :format ?format  .

    VALUES (?format ?mediaType) {
      (format:Audiobook               mediaType:Audiobook)
      (format:Language_course         mediaType:LanguageCourse)
      (format:Music_recording         mediaType:MusicRecording)
      (format:Sheet_music             mediaType:SheetMusic)
      (format:Map                     mediaType:Map)
      (format:Periodical              mediaType:Periodical)
    }
}
;


# 3) Change media type for comic books

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>
PREFIX literaryForm: <http://data.deichman.no/literaryForm#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub :mediaType mediaType:Book .
}

INSERT {
  ?pub :mediaType mediaType:ComicBook .
}

WHERE {
  ?pub :literaryForm literaryForm:comicBook ;
    :mediaType mediaType:Book ;
    a :Publication .
}
;


# 4) Add media type and format for card games and board games

SPARQL
PREFIX : <http://__HOST__/ontology#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>
PREFIX format: <http://data.deichman.no/format#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub :genre <http://__HOST__/genre/g2049303700> .
  ?pub :genre <http://__HOST__/genre/g2049303800> .
}

INSERT {
  ?pub :mediaType mediaType:Game ;
    :format ?format .
}

WHERE {
  ?pub :format format:Physical_body ;
    :genre ?genre ;
    a :Publication .

  VALUES (?genre  ?format) {
    (<http://__HOST__/genre/g2049303700>   format:BoardGame)
    (<http://__HOST__/genre/g2049303800>   format:CardGame)
  }
}
;