# 1) Remove split statements from publications

SPARQL
PREFIX migration: <http://migration.deichman.no/>
PREFIX : <http://data.deichman.no/ontology#>

WITH <http://deichman.no/migration>

DELETE {
  ?publication ?p ?o
} WHERE {
  ?publication a :Publication ;
    ?p ?o .
  VALUES ?p {
    migration:genreSplit1
    migration:genreSplit2
    migration:genreSplitMain
    migration:genreSplitSub
  }
}
;


# 2) Biography genres with subdivision

SPARQL
PREFIX migration: <http://migration.deichman.no/>
PREFIX : <http://data.deichman.no/ontology#>
PREFIX literaryForm: <http://data.deichman.no/literaryForm#>
PREFIX duo: <http://data.deichman.no/duo#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?publication :genre ?genre ;
    migration:genreSplit ?genreSplit .
}

INSERT {
  ?publication :literaryForm literaryForm:biography ;
    :subject `IRI(CONCAT(STR("http://data.deichman.no/subject/s"), STR(MD5(STR(?label)))))` .
  `IRI(CONCAT(STR("http://data.deichman.no/subject/s"), STR(MD5(STR(?label)))))` a :Subject ;
    :prefLabel ?label .
}

WHERE {
  ?genreSplit migration:genreSplitMain <http://data.deichman.no/genre/gf9e7a1a0a6912af8c1227f268ce80be1> ;
    migration:genreSplitSub ?genreSplitSub ;
    duo:bibliofilAuthorityId ?id ;
    a migration:GenreSplit .
  ?genreSplitSub :prefLabel ?label .
  ?publication migration:genreSplit ?genreSplit ;
    :genre ?genre .
  ?genre duo:bibliofilAuthorityId ?id .
}
;


#3) Form + country of origin split

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX duo: <http://data.deichman.no/duo#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
 ?publication :genre ?genre ;
  migration:genreSplit ?genreSplit .
}

INSERT {
  ?publication migration:form ?genreSplit1 ;
    migration:nationOfOrigin ?label .
}

WHERE {
  ?genreSplit migration:genreSplit1 ?genreSplit1 ;
    migration:genreSplit2 ?genreSplit2 ;
    duo:bibliofilAuthorityId ?id ;
    a migration:GenreSplit .
  ?genre duo:bibliofilAuthorityId ?id .
  ?publication migration:genreSplit ?genreSplit ;
    :genre ?genre .
  ?genreSplit2 :prefLabel ?label .
}
;


# 4) Construct literaryForms from split genres)

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX genre: <http://data.deichman.no/genre/>
PREFIX literaryForm: <http://data.deichman.no/literaryForm#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub migration:form ?old .
}

INSERT {
  ?pub :literaryForm ?form .
}

WHERE {
    ?pub migration:form ?old ;
      a :Publication  .

    VALUES (?old ?form) {
      (genre:g8485f1b48ceda0998652c91ebe113ec7         literaryForm:comicBook)
      (genre:gc22bb0ade4c6cf956780a73f4fe2ae82         literaryForm:featureFilm)
      (genre:g00bbd6c64086b4e701f8fb04c4e0b6fd         literaryForm:animatedFilm)
      (genre:g89509789182bc7afe48d315f93e341b6         literaryForm:televisionSeries)
      (genre:g81baf70b3a80d10106e42b69ab6f6144         literaryForm:shortStory)
      (genre:g93341cf23c67289f4646abeeded6a9d8         literaryForm:shortFilm)
      (genre:g3e07a0ff81ac73bad74b53c29380c9cb         literaryForm:poetry)
    }
}
;


# 5) Add correct genre from split genres

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX genre: <http://data.deichman.no/genre/>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub migration:form ?old .
}

INSERT {
  ?pub :genre ?genre .
}

WHERE {
    ?pub migration:form ?old ;
      a :Publication .

    VALUES (?old ?genre) {
      (genre:g0a15256825b1da3e6957a7be856da85c         genre:g12161100)
      (genre:g612d9660fee4280d8884399d83051931         genre:g12557900)
      (genre:g0f21d4e5a7cf29a1ece03d5c32bbc923         genre:g13003700)
      (genre:ga778c7237fbff6e342574fe7bbcac62b         genre:g15868000)
      (genre:g8d8cc2364ee5b8456db9d2327f4d2e83         genre:g15868000)
    }
}
;


# 6) Add genres from subdivisions 

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX genre: <http://data.deichman.no/genre/>
PREFIX duo: <http://data.deichman.no/duo#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?doc :genre ?genre .
  ?genre ?p ?o .
}

INSERT {
  ?doc :genre ?splitSub .
}

WHERE {
  ?split migration:genreSplitMain ?splitMain ;
    migration:genreSplitSub ?splitSub ;
    duo:bibliofilAuthorityId ?id ;
    a migration:GenreSplit .
  ?genre duo:bibliofilAuthorityId ?id ;
    a :Genre ;
    ?p ?o .
  ?doc :genre ?genre ;
    a :Publication .

  VALUES ?splitMain {
    genre:ge0b8be8ffba2e9d06835d2f508501be8
    genre:g00bbd6c64086b4e701f8fb04c4e0b6fd
    genre:ge0b8be8ffba2e9d06835d2f508501be8
    genre:g8485f1b48ceda0998652c91ebe113ec7
  }
}
;


# 7) Merge duplicate genres with specification

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX duo: <http://data.deichman.no/duo#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?publication :genre ?genreDelete .
  ?genreDelete ?p ?o .
}

INSERT {
  ?publication :genre ?genreKeep .
}

WHERE {
  ?genreKeep a :Genre ;
    :specification ?spec ;
    duo:bibliofilAuthorityId ?id1 ;
    :prefLabel ?label .
  ?genreDelete :prefLabel ?label ;
    :specification ?spec ;
    duo:bibliofilAuthorityId ?id2 ;
    a :Genre ;
    ?p ?o .
  ?publication :genre ?genreDelete ;
    a :Publication .
  FILTER (?genreKeep != ?genreDelete)
  FILTER (xsd:int(?id1) < xsd:int(?id2))
}
;


# 8) Merge duplicate genres without specification

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX duo: <http://data.deichman.no/duo#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?publication :genre ?genreDelete .
  ?genreDelete ?p ?o .
}

INSERT {
  ?publication :genre ?genreKeep .
}

WHERE {
  ?genreKeep a :Genre ;
    duo:bibliofilAuthorityId ?id1 ;
    :prefLabel ?label .
  ?genreDelete :prefLabel ?label ;
    duo:bibliofilAuthorityId ?id2 ;
    a :Genre ;
    ?p ?o .
  ?publication :genre ?genreDelete ;
    a :Publication .
  MINUS {?genreDelete :specification ?spec .}
  MINUS {?genreKeep :specification ?spec .}
  FILTER (?genreKeep != ?genreDelete)
  FILTER (xsd:int(?id1) < xsd:int(?id2))
}
;


# 9) Merge duplicate genres without autority id

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX migration: <http://migration.deichman.no/>
PREFIX duo: <http://data.deichman.no/duo#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?publication :genre ?genreWithout .
  ?genreWithout ?p ?o .
}

INSERT {
  ?publication :genre ?genreWith .
}

WHERE {
  ?genreWith a :Genre ;
    duo:bibliofilAuthorityId ?id ;
    :prefLabel ?label .
  ?genreWithout :prefLabel ?label ;
    a :Genre ;
    ?p ?o .
  ?publication :genre ?genreWithout ;
    a :Publication .
  MINUS {?genreWith :specification ?spec .}
  MINUS {?genreWithout duo:bibliofilAuthorityId ?any .}
}
;
