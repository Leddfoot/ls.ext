PREFIX deich: <__ONTOLOGY__>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  role: <http://data.deichman.no/role#>

CONSTRUCT {
        ?pub a deich:Publication ;
               deich:mainTitle ?pubMainTitle ;
               deich:partTitle ?pubPartTitle ;
               deich:norwegianTitle ?norwegianTitle ;
               deich:englishTitle ?enlishTitle ;
               deich:language ?language ;
               deich:format ?format ;
               deich:publicationYear ?pubPublicationYear ;
               deich:firstPublicationYear ?workPublicationYear ;
               deich:audience ?audience ;
               deich:hasHoldingBranch ?branch ;
               deich:contributor ?contrib ;
               deich:hasImage ?img ;
               deich:hasImageAll ?allImg ;
               deich:workUri ?workUri ;
               deich:subject ?subjectName ;
               deich:audience ?audience ;
               deich:agents ?contribAgentName ;
               ?contribRoleURI ?contribAgentName ;
               deich:mainEntryName ?mainEntryName ;
               deich:recordID ?recordId ;
               deich:abstract ?abstract .
    ?contrib a deich:Contribution ;
               deich:role ?contribRole ;
               deich:agent ?contribAgent ;
               deich:mainEntry ?mainEntry .
?contribAgent a deich:Agent ;
               deich:name ?contribAgentName ;
               deich:birthYear ?contribAgentBirthYear ;
               deich:deathYear ?contribAgentDeathYear .
      ?subject deich:name ?subjectLabel .
}
WHERE {
  BIND(uri("__PUBLICATIONURI__") AS ?pub)
  ?pub a deich:Publication ;
       deich:publicationOf ?work ;
       deich:recordID ?recordId .

  BIND(str(?work) AS ?workUri )

  OPTIONAL { ?work deich:audience ?audienceO . BIND(str(?audienceO) AS ?audience) }
  OPTIONAL { ?work deich:publicationYear ?workYear . BIND(str(?workYear) AS ?workPublicationYear) }

  OPTIONAL {
    ?p deich:publicationOf ?work ;
       deich:mainTitle ?norwegianMainTitle ;
       deich:language <http://lexvo.org/id/iso639-3/nob> .
    OPTIONAL {
    ?p deich:publicationOf ?work ;
           deich:partTitle ?norwegianPartTitle ;
           deich:language <http://lexvo.org/id/iso639-3/nob> .
    }
    BIND(IF(BOUND(?norwegianPartTitle), CONCAT(?norwegianMainTitle, " — ", ?norwegianPartTitle), ?norwegianMainTitle) AS ?norwegianTitle)
  }

  OPTIONAL {
    ?p deich:publicationOf ?work ;
       deich:mainTitle ?englishMainTitle ;
       deich:language <http://lexvo.org/id/iso639-3/eng> .
    OPTIONAL {
        ?p deich:publicationOf ?work ;
               deich:partTitle ?englishPartTitle ;
               deich:language <http://lexvo.org/id/iso639-3/eng> .
        }
        BIND(IF(BOUND(?englishPartTitle), CONCAT(?englishMainTitle, " — ", ?englishPartTitle), ?englishMainTitle) AS ?norwegianTitle)
  }

  OPTIONAL {
      ?p deich:publicationOf ?work ;
         deich:hasImage ?allImg.
    }

  OPTIONAL {
    {
      ?work deich:subject ?subject .
      ?subject a deich:Subject ;
              deich:prefLabel ?subjectName  .
    } UNION {
      ?work deich:subject ?subject .
            ?subject a deich:Place ;
                    deich:prefLabel ?subjectName  .
    } UNION {
      ?work deich:subject ?subject .
      ?subject a deich:Work ;
              deich:mainTitle ?subjectName  .
    } UNION {
      ?work deich:subject ?subject .
      ?subject a deich:Person ;
              deich:name ?subjectName  .
    }
    OPTIONAL { ?work deich:subject ?subject . ?subject deich:specification ?subjectSpec }
    BIND(IF(BOUND(?subjectSpec), CONCAT(?subjectName, " (", ?subjectSpec, ")"), ?subjectName) AS ?subjectLabel)
  }

  OPTIONAL { ?pub deich:mainTitle ?pubMainTitle }
  OPTIONAL { ?pub deich:partTitle ?pubPartTitle }
  OPTIONAL { ?pub deich:hasHoldingBranch ?branch }
  OPTIONAL {
             { ?pub deich:contributor ?contrib } UNION { ?work deich:contributor ?contrib }
             ?contrib a deich:Contribution ;
                      deich:agent ?contribAgent ;
                      deich:role ?contribRoleURI .
             ?contribAgent deich:name ?contribAgentName .
             BIND(str(?contribRoleURI) AS ?contribRole)
             OPTIONAL {
                ?contrib a deich:MainEntry .
                BIND(true as ?mainEntry) .
                ?contribAgent deich:name ?mainEntryName .
             }
  }

  OPTIONAL { ?contribAgent deich:birthYear ?pBirthYearO . BIND(str(?pBirthYearO) AS ?contribAgentBirthYear) }
  OPTIONAL { ?contribAgent deich:deathYear ?pDeathYearO . BIND(str(?pDeathYearO) AS ?contribAgentDeathYear) }

  OPTIONAL { ?pub deich:language ?languageO . BIND(str(?languageO) AS ?language) }
  OPTIONAL { ?pub deich:format ?formatO . BIND(str(?formatO) AS ?format) }
  OPTIONAL { ?pub deich:publicationYear ?pubPublicationYearO . BIND(str(?pubPublicationYearO) AS ?pubPublicationYear) }
  OPTIONAL { ?pub deich:hasImage ?img }
  OPTIONAL { ?pub deich:abstract ?abstract }
}
