PREFIX  :     <__ONTOLOGY__>

CONSTRUCT
  { ?serialUri <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> :Serial ;
               :mainTitle ?mainTitle ;
               :subtitle ?subtitle ;
               :partTitle ?partTitle ;
               :partNumber ?partNumber ;
               :publishedByName ?publishedByName ;
               :publishedByAlternativeName ?publishedByAlternativeName ;
               :publishedByPlacePrefLabel ?publishedByPlacePrefLabel ;
               :publishedByPlaceAlternativeName ?publishedByPlaceAlternativeName .
  }
WHERE
  { SELECT DISTINCT  ?serialUri ?mainTitle ?subtitle ?publishedByName ?publishedByAlternativeName ?publishedByPlacePrefLabel ?publishedByPlaceAlternativeName ?partTitle ?partNumber
    WHERE
      { BIND(uri("__SERIALURI__") AS ?serialUri)
        ?serialUri  a                   :Serial ;

        OPTIONAL
          { ?serialUri  :mainTitle  ?mainTitle
          }
        OPTIONAL
          { ?serialUri  :subtitle  ?subtitle
          }
        OPTIONAL
          { ?serialUri  :partTitle  ?partTitle
          }
        OPTIONAL
          { ?serialUri  :partNumber ?partNumber
          }
        OPTIONAL
          {
            ?serialUri  :publishedBy  ?publishedByUri .
            ?publishedByUri :name ?publishedByName .
            OPTIONAL {
                ?publishedByUri :alternativeName ?publishedByAlternativeName .
            }
            OPTIONAL {
                ?publishedByUri :place ?publishedByPlaceUri .
                ?publishedByPlaceUri a :Place ;
                                     :prefLabel ?publishedByPlacePrefLabel .
                OPTIONAL {
                    ?publishedByPlaceUri :alternativeName ?publishedByPlaceAlternativeName .
                }

            }
          }
      }
  }