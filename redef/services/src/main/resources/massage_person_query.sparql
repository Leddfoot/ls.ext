PREFIX  deich:     <__ONTOLOGY__>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  duo:  <http://data.deichman.no/utility#>

CONSTRUCT {
  ?person a deich:Person ;
          deich:name ?name ;
          deich:birthYear ?birthYear ;
          deich:deathYear ?deathYear ;
          deich:ordinal ?ordinal ;
          deich:specification ?specification ;
          deich:nationality ?nationalityLabel ;
          deich:work ?work .
    ?work a deich:Work ;
          deich:mainTitle ?mainTitle ;
          deich:partTitle ?partTitle ;
          deich:publicationYear ?publicationYear ;
          deich:contribRoleName ?contribRoleName .
}
WHERE {
  BIND(uri("__PERSONURI__") AS ?person)
  ?person a deich:Person .

  OPTIONAL { ?person deich:name ?name }
  OPTIONAL { ?person deich:birthYear ?birthYearO . BIND(str(?birthYearO) AS ?birthYear) }
  OPTIONAL { ?person deich:deathYear ?deathYearO . BIND(str(?deathYearO) AS ?deathYear) }
  OPTIONAL { ?person deich:ordinal ?ordinalO . BIND(str(?ordinalO) AS ?ordinal) }
  OPTIONAL { ?person deich:specification ?specificationO . BIND(str(?specificationO) AS ?specification) }
  OPTIONAL {
       ?person deich:nationality ?nationality .
       ?nationality rdfs:label ?nl .
       FILTER (lang(?nl) = "no")
       BIND(str(?nl) AS ?nationalityLabel)
  }
  OPTIONAL {
       ?work a deich:Work ;
             deich:contributor ?contrib .
    ?contrib deich:agent ?person ;
             deich:role ?contribRole .
    ?contribRole rdfs:label ?contribRoleNameNo .
       FILTER (lang(?contribRoleNameNo) = "no")
       BIND(str(?contribRoleNameNo) AS ?contribRoleName)
  }
  OPTIONAL { ?work deich:mainTitle ?mainTitle }
  OPTIONAL { ?work deich:partTitle ?partTitle }
  OPTIONAL { ?work deich:publicationYear ?publicationYearO . BIND(str(?publicationYearO) AS ?publicationYear) }
}
