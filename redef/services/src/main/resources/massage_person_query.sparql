PREFIX  :     <__ONTOLOGY__>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  duo:  <http://data.deichman.no/utility#>

CONSTRUCT
  { ?personUri <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> :Person .
    ?personUri :name ?name .
    ?personUri :birthYear ?birthYear .
    ?personUri :deathYear ?deathYear .
    ?personUri :nationality ?nationality .
    ?personUri :work ?work .
    ?work <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> :Work .
    ?work :mainTitle ?mainTitle .
    ?work :partTitle ?partTitle .
    ?work :publicationYear ?publicationYear .
  }
WHERE
  { SELECT DISTINCT  ?personUri ?name ?nationality ?birthYear ?deathYear ?work ?mainTitle ?partTitle ?publicationYear
    WHERE
      { BIND(uri("__PERSONURI__") AS ?personUri)
        ?personUri  a                   :Person .
        OPTIONAL
          { ?personUri      :name                 ?name }
        OPTIONAL
          { ?personUri  :birthYear  ?birthYearO
            BIND(str(?birthYearO) AS ?birthYear)
          }
        OPTIONAL
          { ?personUri  :deathYear  ?deathYearO
            BIND(str(?deathYearO) AS ?deathYear)
          }
        OPTIONAL
          { ?personUri  :nationality  ?nationalityO .
            BIND(str(?nationalityO) AS ?nationality)
          }
        OPTIONAL
          { ?work  a                     :Work ;
                   :contributor ?contrib .
            ?contrib        :agent              ?personUri
          }
        OPTIONAL
          { ?work  :mainTitle  ?mainTitleO
            BIND(str(?mainTitleO) as ?mainTitle)
          }
        OPTIONAL
          { ?work  :partTitle  ?partTitleO
            BIND(str(?partTitleO) as ?partTitle)
          }
        OPTIONAL
          { ?work  :publicationYear  ?publicationYearO
            BIND(str(?publicationYearO) AS ?publicationYear)
          }
      }
  }
