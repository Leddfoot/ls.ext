apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'cpd'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

group = 'no.deichman.services'
version = '1.0-SNAPSHOT'

description = "services"

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven {
        url "http://xss-html-filter.sf.net/releases/"
        name "xss-html-filter.sf.net releases"
    }
    maven {
        url "https://maven.nuxeo.org/nexus/content/groups/public/"
        name "nexeo"
    }
}

sourceSets {
    moduleTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    moduleTestCompile.extendsFrom testCompile
    moduleTestRuntime.extendsFrom testRuntime

    // We're logging with slf4j api and log4j2 backend so all other backends must be removed
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'org.slf4j', module: 'slf4j-nop'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'commons-logging'

}

def version_jetty = '9.2.10.v20150310'
def version_jersey = '2.21'
def version_jackson = '2.6.2'
def version_slf4j = '1.7.12'
def version_jena = '3.0.0'
def version_commons_lang = '2.6'
def version_commons_io = '2.4'

dependencies {

    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: version_jetty
    compile group: 'org.eclipse.jetty', name: 'jetty-util', version: version_jetty
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: version_jetty
    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: version_jersey
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version: version_jersey
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-http', version: version_jersey
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-moxy', version: version_jersey
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: version_jackson
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-smile', version: version_jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: version_jackson
    compile group: 'org.apache.jena', name: 'apache-jena-libs', version: version_jena
    compile group: 'com.github.jsonld-java', name: 'jsonld-java', version: '0.5.1'
    compile group: 'commons-io', name: 'commons-io', version: version_commons_io
    compile group: 'commons-lang', name: 'commons-lang', version: version_commons_lang
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.0'
    compile ('info.freelibrary:freelib-marc4j:2.6.7') {
        transitive = false
    }
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'com.google.code.gson',name: 'gson', version: '2.3.1'

    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.3+' // unirest has issues with 4.2
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.3+' // unirest has issues with 4.2
    compile group: 'org.apache.httpcomponents', name: 'httpclient-cache', version: '4.3+' // unirest has issues with 4.2

    // We're logging with slf4j api and log4j2 backend
    compile group: 'org.slf4j', name: 'slf4j-api', version: version_slf4j
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: version_slf4j
    compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: version_slf4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.3'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.3'
    compile group: 'com.jamonapi', name: 'jamon', version: '2.81'
    compile group: 'org.eclipse.jetty', name: 'jetty-webapp', version: version_jetty
    compile group: 'org.eclipse.jetty', name: 'jetty-annotations', version: version_jetty
    compile group: 'org.eclipse.jetty', name: 'apache-jsp', version: version_jetty
    compile group: 'net.sf.xss-html-filter', name: 'xss-html-filter', version: '1.5'
    compile group: 'com.jamonapi', name: 'fdsapi', version: '1.2'
    compile group: 'oro', name: 'oro', version: '2.0.8'


    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.0.31-beta'
    testCompile group: 'com.github.rest-driver', name: 'rest-client-driver', version: '1.1.42'
    testCompile group: 'uk.co.datumedge', name: 'hamcrest-json', version: '0.2'

    moduleTestCompile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.7'
    moduleTestCompile group: 'org.elasticsearch', name: 'elasticsearch', version: '2.1.0', classifier: 'tests'
    moduleTestCompile group: 'org.elasticsearch', name: 'elasticsearch', version: '2.1.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

task moduleTest(type: Test) {
    testClassesDir = sourceSets.moduleTest.output.classesDir
    classpath = sourceSets.moduleTest.runtimeClasspath
}

moduleTest.mustRunAfter test
check.dependsOn moduleTest

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

checkstyle {
    configProperties = [samedir: "${rootDir}"]
    // Cannot go higher as 6.8 breaks compatibility with the current Gradle checkstyle plugin
    // See change to CheckStyleTask here: http://checkstyle.sourceforge.net/releasenotes.html#Release_6.8
    toolVersion = 6.7
}

pmd {
    consoleOutput = true
    targetJdk = 1.7 // 1.8 is yet unsupported
    toolVersion = '5.3.3'
}

cpd {
    toolVersion = '5.3.3'
    ignoreIdentifiers = true
    ignoreLiterals = true

    ignoreFailures = true // not failing the build for now
}

cpdCheck {
    reports {
        text.enabled = true
        xml.enabled = false
    }
}

idea {
    module {
        scopes.TEST.plus += [ configurations.moduleTestCompile ]
    }
}

buildscript {
    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath 'de.aaschmid.gradle.plugins:gradle-cpd-plugin:0.5'
        classpath 'com.bmuschko:gradle-docker-plugin:2.6.4'
    }
    repositories {
        mavenCentral()
        maven {
            url '~/.m2/repository'
        }
        jcenter()
    }
}

mainClassName = 'no.deichman.services.App'

docker {
    url = "http://172.17.0.1:2375"

    registryCredentials {
        url = 'https://index.docker.io/v1'
        username = 'deichman'
        password = 'paaTryggGrunnForFremtiden'
    }
    javaApplication {
        maintainer = 'digitalutvikling@gmail.com'
        port = 8005
        tag = "digibib/redef-services"
    }
}

